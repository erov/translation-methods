e -> t e_prime {
    e_0.result = e_prime_2.result
    e_prime_2.lhs = t_1.result
}
e_prime -> '+' t $ADD e_prime {
    $ADD.op0 = e_prime_0.lhs
    $ADD.op1 = t_2.result
    e_prime_4.lhs = $ADD.result
    e_prime_0.result = e_prime_4.result
}
e_prime -> '-' t $SUB e_prime {
    $SUB.op0 = e_prime_0.lhs
    $SUB.op1 = t_2.result
    e_prime_4.lhs = $SUB.result
    e_prime_0.result = e_prime_4.result
}
e_prime -> '' {
    e_prime_0.result = e_prime_0.lhs
}
t -> f t_prime {
    t_0.result = t_prime_2.result
    t_prime_2.lhs = f_1.result
}
t_prime -> '*' f $MUL t_prime {
    $MUL.op0 = t_prime_0.lhs
    $MUL.op1 = f_2.result
    t_prime_4.lhs = $MUL.result
    t_prime_0.result = t_prime_4.result
}
t_prime -> '/' f $DIV t_prime {
    $DIV.op0 = t_prime_0.lhs
    $DIV.op1 = f_2.result
    t_prime_4.lhs = $DIV.result
    t_prime_0.result = t_prime_4.result
}
t_prime -> '' {
    t_prime_0.result = t_prime_0.lhs
}
f -> n fact {
    fact_2.arg = n_1.result
    f_0.result = fact_2.result
}
f -> '(' e ')' fact {
    fact_4.arg = e_2.result
    f_0.result = fact_4.result
}
n -> [0-9]+
fact -> '' {
    fact_0.result = fact_0.arg
}
fact -> '!' $FACT fact {
    $FACT.op0 = fact_0.arg
    fact_3.arg = $FACT.result
    fact_0.result = fact_3.result
}